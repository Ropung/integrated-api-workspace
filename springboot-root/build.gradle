plugins {
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version '2.7.12'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
	group = 'com.joara'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	repositories {
		mavenCentral()
	}

	bootJar {
		enabled = false
	}
}

subprojects {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'

		// Lombok
		compileOnly 'org.projectlombok:lombok'
		testCompileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'

		// Logging
		implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
		implementation 'org.apache.logging.log4j:log4j-core:2.19.0'

		// JSON
		implementation 'com.google.code.gson:gson:2.10.1'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

// === Auth Service ====================================================================================================

// NOTE this module is for execution. All required beans must be included.
project(':authentication-service') {
	dependencies {
		implementation project(':authentication-service:application')
		implementation project(':authentication-service:adapters:rdb-adapter')
		implementation project(':authentication-service:adapters:web-adapter')
		implementation project(':common')
		implementation project(':base-modules:joara-global-exception-handler')
		implementation project(':base-modules:joara-integrated-flyway')

		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		runtimeOnly 'org.postgresql:postgresql'
	}

	bootJar {
		enabled = true
	}
}

project(':authentication-service:adapters:rdb-adapter') {
	dependencies {
		implementation project(':common')
		implementation project(':authentication-service:application')
		implementation project(':base-modules:joara-jpa-base')
		implementation project(':base-modules:joara-integrated-flyway')

		// Map Struct
		implementation 'org.mapstruct:mapstruct:1.5.3.Final'
		annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
		annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	}
}

project(':authentication-service:application') {
	dependencies {
		implementation project(':common')
		api project(':authentication-service:application:domain')
		api 'org.springframework.data:spring-data-commons' // 여기에 있는 Page 등의 기능을 ...
	}
}

project(':authentication-service:application:domain') {
	dependencies {
		implementation project(':common')
	}
}

// === Book Service ====================================================================================================

project(':book-service') {
	dependencies {
		implementation project(':common')
		implementation project(':base-modules:joara-global-exception-handler')
		implementation project(':base-modules:joara-integrated-flyway')
	}
}

// === Base Modules ====================================================================================================

project(':base-modules:joara-jpa-base') {
	dependencies {
		api 'org.springframework.boot:spring-boot-starter-data-jpa'
	}
}

project(':base-modules:joara-global-exception-handler') {
	dependencies {
		api project(':common')
	}
}

project(':base-modules:joara-integrated-flyway') { // 초창기 전략: 통합 DB 채택 -> ... (대신 이것을 유지하는 동안 모든 서버 재배포) -> 점진적으로 디비를 나눠 가는 선택을 할 수 있음.
	dependencies {
		api 'org.flywaydb:flyway-core' // api는 비권장. 그럼에도 불구하고 쓴 이유? -> 사람이 심장은 달고 다녀야 함.
	}
}